\chapter{Überblick über den Kern der Engine}
\label{chap:ueberblick-pipeline}

Auf Grund des Umfangs des Projekts ist es nicht möglich einen all umfassenden Überblick über die Engine zu geben. Die Betrachtung im Detail beschränk sich im Folgenden auf die entwickelte Renderkomponente der Engine und die dahinter stehenden Konzepte und Überlegungen.

\section{Das Rendersystem}

% anforderung
Moderne Renderverfahren (wie zum Beispiel \warn{Deferred Rendering}) bestehen aus mehreren einzelnen Renderschritten. In der Regel erzeugt jeder Renderschritt Daten, oft in Form von Texturen oder Buffern, die dann im fortlaufenden Verfahren als Basis für weitere Renderschritte dienen.
% Netzwerk

% Ziel
% - äußere logik komponierbar / flexibilät
% - vermeidung von Implementierungdetails in der äußeren logik
% - implementierungen nah an opengl (verzicht auf abstraktionsschicht)

Die Konzeption des Rendersystem wurde mit dem Ziel verfolgt die einzelnen Renderschritte eines modernen verzögerten Rendersystems 

\subsection{Mealy als formale Basis}

Das Rendersystem basiert auf dem Konzept eines Mealy Automatens \parencite{Mealy1955}. Ein Mealy Automat ist \ac{FST}, ein spezieller endlicher Automat, der nicht nur seine Eingabesprache vom Eingabeband akzeptiert sondern auch eine Ausgabe auf einem Ausgabeband erzeugt. Ein Transduktor übersetzt die Eingabesprache in eine Ausgabesprache. Bei dem Mealy Automaten als spezielle Form eines \ac{FST} definiert sich die Ausgabe über den momentanen Zustand und die Eingabe auf diesem Zustand. Formal lässt sich ein Mealy Automat $\mathbfcal{M}$ als 6-Tupel wie folgt definieren:

\begin{align}
\mathbfcal{M} = \left( Q, \Sigma, \Omega, \delta, \lambda, q_0 \right)
\label{def:mealy-formal}
\end{align}
\begin{align*}
	\text{mit}\\
	Q &: \text{Endliche Menge von Zuständen} \\
	\Sigma  &:\text{Endliches Eingabealphabet} \\
	\Omega  &:\text{Endliches Ausgabealphabet} \\
	\delta  &:\text{Zustandsübergangsfunktion}\ Q \times \Sigma \rightarrow Q \\
	\lambda &:\text{Ausgabefunktion}\ Q \times \Sigma \rightarrow \Omega \\
	q_0 &: \text{Startzustand}
\end{align*}

Die Definition ließe sich noch um die Menge der finalen Endzustände $F \subseteq Q$ hin zu einem 7-Tupel erweitern. In unserem Konzept verzichten wir auf definierte Endzustände, da wir prinzipiell jede Eingabe akzeptieren. Nicht akzpetierbare Eingaben stellen wir als externe Ausnahmefälle (Exceptions) dar. Ein vereinfachtes Beispiel für eine nicht akzeptierbare Eingabe ist die Eingabe $\bot$, nach der unser gesamtes System in einen undefinierten Zustand $\bot$ wechselt.

\paragraph{Abstrakte Definition in Haskell}
\label{sec:abstrakte-definition-haskell}

Ein Mealy Automat lässt sich in Haskell abstrakt wie in \fref{lst:haskell-mealy} definieren. {\ttfamily a} beschreibt die Eingabe als Äquivalent zum Eingabealphabet $\Sigma$ der formalen Definition und analog beschreibt |b| die Ausgabe als Äquivalent zum Ausgabealphabet $\Omega$. Die Zustandsübergangsfunktion $\delta$ und die Ausgabefunktion $\lambda$ ergibt sich aus der jeweils konkreten Implementierung des Mealy. Die Menge der Zustände $Q$ wird jeweils von der konkreten Mealy Implementierung gekapselt wie in \fref{lst:state-mealy-beispiel} anhand eines Beispiels veranschaulicht. Aber es lässt sich bereits feststellen, dass die gewählte Form des Mealy Automatens weit mehr als nur ein endlicher Automat ist, da die Mächtigkeit des Ein- und Ausgabealphabets unendlich sein kann, wie dies zum Beispiel bei der Menge der natürlichen Zahlen (abzählbar unendlich) der Fall ist. Daraus folgt zusätzlich, dass auch die Menge der Zustände unendlich sein kann (Zählmaschine).

\begin{haskell}[label={lst:haskell-mealy},caption={[Definition Mealy in Haskell]Definition Mealy in Haskell\protect\footnotemark}]
newtype Mealy a b = Mealy { runMealy :: a -> (b, Maely a b) }
\end{haskell}
\footnotetext{https://hackage.haskell.org/package/machines-0.4.1/docs/Data-Machine-Mealy.html}

Für diese Definition einer Meleay lassen sich einige nützlicher Klassen finden, die wir aber hier überspringen da wir in \fref{sec:konkret-rendersystem} für unsere erweiterte monadische Definition eigene Implementierungen entwickeln werden.

Allgemein lässt sich die Funktionsweise von |Mealy| wie folgt beschreiben: Zu jeder Eingabe |a| erzeugt die Ausfühung von |Mealy| durch |runMealy| eine Ausgabe |b| und einen neuen Mealy-Automaten, der die selbe Eingabesprache in die selbe Ausgabesprache übersetzt. Im einfachsten Fall ist der neu erzeugte Mealy-Automat exakt der gleiche aus dem vorherigen Schritt. Ist dies für das gesamte Eingabealphabet gegeben gilt der Automat als zustandslos.

Für die Konstruktion eines Mealy-Automatens mit lokalem Zustand folgt aus der formalen Definition \ref{def:mealy-formal} die Notwendigkeit einer Zustandsübergangsfunktion $\delta$ und einem Startzustand $q_0$. Hiefür definieren wir uns in \fref{lst:state-mealy-ctr} die Hilfsfunktion |localStateMealy|. Der erste Parameter |s -> a -> (b, s)| beschreibt die Kombination der beiden Funktionen $\delta$ und $\lambda$, der zweite Parameter den Startzustand $q_0$.

\begin{haskell}[label={lst:state-mealy-ctr},caption={[Konstruktion Mealy mit lokalem Zustand]Hilfsfunktion zur Konstruktion eines Mealy-Automatens mit lokalem Zustand}]
localStateMealy :: (s -> a -> (b, s)) -> s -> Mealy a b
localStateMealy stateTransition initState = run initState where
  run state = Mealy (\input -> 
    let (output, state') = stateTransition state input 
    in (output, stateTransition state'))
\end{haskell}

Mit Hilfe der Funktion |localStateMealy| wird ein Mealy-Automat erzeugt, der bei jeder Ausführung die |stateTransition| Funktion auf die aktuellen Eingabe |input| und den momentanen Zustand |state| anwendet um eine Ausgabe |output| und einen neuen Zustand |state'| zu erzeugen. |output| wird in Verbindung mit dem für die nächste Ausführung neu konstruiertem Meleay-Automaten als Resultat zurück geliefert.

In \fref{lst:state-mealy-beispiel} konstruieren wir einen einfachen Mealy-Automaten der ganzzahlige Eingaben akkumuliert und ausgibt. Abschließend wird in \fref{lst:state-mealy-ausfuehrung} ein Ausführungsbeispiel für |sumMealy| demonstriert.

\begin{haskell}[label={lst:state-mealy-beispiel},caption={[Beispiel Mealy-Automat mit lokalem Zustand]Beispiel Mealy-Automat mit lokalem Zustand}]
sumMealy :: Mealy Int Int
sumMealy = localStateMealy sumInState 0 where
	sumInState state input = (state, state + input)
\end{haskell}

\begin{haskell}[label={lst:state-mealy-ausfuehrung},nolol,caption={Ausführung Mealy-Automat mit lokalem Zustand}]
> let m0 = sumMealy
> let m1 = runMealy m 5
> fst m1
0
> let m2 = runMealy m1 8
> fst m2
5
> let m3 = runMealy m2 0
> fst m3
13
\end{haskell}

\subsection{Konkrete Definition des Rendersystems}
\label{sec:konkret-rendersystem}

\subsubsection{Anforderung}
Ein konkretes Rendersystem soll sich aus mehreren \warn{isolierten} Renderschritten, den Renderpasses, zusammensetzen. Durch die rekursive Definition kann ein Renderpass wiederum ein beliebig komplexes Rendersystem darstellen, so dass zwischen Rendersystem und Renderpass nur semantisch unterschieden wird. Jeder konkrete Pass soll einen konkret definierten Eingabetypen und einen konkret definierten Ausgabetypen besitzen. Ein Renderpass übersetzt die Eingabe in eine Ausgabe und kapselt alle dafür notwendigen Zustände und Vorgänge. Zustandsveränderungen sollen nicht direkt von außen vorgenommen werden können. Zu dem internen Zustand eines Renderpasses können unter anderem benötigte Ressourcen wie Framebuffer oder Shader gehören. Der interne Zustand soll nur vom Renderpass selbst sichbar gemacht werden können, und dass auch nur, wenn (ein Teil-) Zustand vom Renderpass in die Ausgabe geleitet wird. Direkte Veränderungen des internen Zustands sind so von vornherein ausgeschlossen, da in Haskell Daten unveränderlich sind \footnote{ausgenommen über Konstrukte wie MVars oder IORefs}.

Die Definition des Mealy-Typens aus \fref{lst:haskell-mealy} dient als gedankliche Grundlage für die folgende Definition des Rendersystem, da wir mit \ref{lst:state-mealy-ctr} gezeigt haben, wie sich Zustände kapseln lassen. Aus den Anforderungen an das Rendersystem OpenGL-Methoden aufzurufen zu können ergibt sich, dass wir das Konzept des Mealy-Typen noch um einen monadischen Basistypen erweitern müssen, damit wir beispielsweise Operationen in der |IO| Monade ausführen können. Damit wir uns auf keinen konkreten monadischen Typen festlegen müssen, konstruieren wir unser Rendersystem als einen monadischen Transformator (Monad-Transformer). Die Form des monadischen Transformators erlaubt es Operationen aus einer anderen Monade, der Basismonade, in unsere Ausführung zu heben (lift). So können wir unserem Rendersystem zum Beispiel über eine |Writer|-Monade die Fähigkeit verleihen Vorgänge zu protokollieren oder über deine |Reader|-Monade können sich die Renderschritte eine unveränderliche Umgebung (Environment) teilen. Durch das sogenannte Stacken von Monaden ist es möglich die Eigenschaften der unterschiedlichsten Monaden unter einer Monade zusammenzufassen, es entsteht ein Monad-Stack.

\subsubsection{Definition}

Aus den oben gegebenen Anforderungen kann jetzt die Definition für |RenderSystem| in \fref{lst:definition-rendersystem} formuliert werden. |m| entspricht der Basismonade, |i| ist der Typparameter für die Eingabe in das Rendersystem und respektive |o| der Typparameter für die Ausgabe. Das Ausführen eines |RenderSystems| liefert, im Gegensatz zum Mealy-Automaten, als Ergebnis eine monadische Operation mit der Ausgabe |o| und dem neuen |RenderSystem| für die nächste Ausführung. In \fref{lst:rendersystem-beispiel-reader} wird ein einfaches zustandsloses |RenderSystem| auf Basis der |Reader|-Monade definiert und in \fref{lst:rendersystem-ausfuehrung-beispiel} exemplarisch ausgeführt. Das |RenderSystem| gibt den momentanen Integerwert der Umgebung auf stdout aus und liefert den Eingabewert unverändert zurück. Der Integerwert der Reader-Monade könnte zum Beispiel einen globalen Frame-Counter repräsentieren.

\begin{haskell}[label={lst:definition-rendersystem},caption={Definition Rendersystem}]
newtype RenderSystem m i o = RenderSystem { runRenderSystem :: i -> m (o, RenderSystem m i o) }
type RenderPass = RenderSystem -- nur ein semantischer Alias
\end{haskell}

\begin{haskell}[label={lst:rendersystem-beispiel-reader},caption={Beispiel Rendersystem mit ReaderT IO als Basismonade}]
type PrintPass = RenderPass (ReaderT IO Int) a a
printFrame :: PrintPass a a
printFrame = RenderSystem (\i -> do
	env <- ask 
	print (show ask)
	return (i,printFrame))
\end{haskell}

\begin{haskell}[label={lst:rendersystem-ausfuehrung-beispiel},caption={Ausführungsbeispiel Rendersystem}]
> let r0 = runReaderT (runRenderSystem printFrame "Hallo") 0
> fst r0
0
"Hallo"
> let r1 = runReaderT (runRenderSystem r0 "Welt!") 1
> fst r1
1
"Welt"
\end{haskell}

Da wir in der praxis unterschiedliche Formen von |RenderPass|es konstruieren werden, definieren wir uns in \fref{lst:renderpass-ctr} Hilffunktionen für die Konstruktion der drei wesentlichen Ausprägungen: 1. zustandslos und unveränderlich, 2. zustandsbehaftet unveränderlich und 3. voll dynamisch.

\begin{haskell}[label={lst:renderpass-ctr},caption={Konstruktoren für einen RenderPass}]
-- 1. zustandslos und unveraenderlich
mkStaticRenderPass :: Monad m => (i -> m o) -> RenderPass m i o
mkStaticRenderPass f = r where r = RenderSystem (liftM (,r) . f)

-- 2. zustandsbehaftet und unveraenderlich
mkStatefulRenderPass :: Monad m => (s -> i -> m (o,s)) -> s -> RenderPass m i o
mkStatefulRenderPass f = go where
  go s = RenderSystem (\i -> do
    (o, t) <- f s i
    return (o, go t))

-- 3. voll dynamisch
mkDynamicRenderPass :: (i -> m (o, RenderPass m i o)) -> RenderPass m i o
mkDynamicRenderPass = RenderSystem
\end{haskell}


%%%
% Klassen
%%%

\subsubsection{Klassen-Instanzen}
\label{sec:rendersystem-klassen-instanzen}

In \fref{sec:abstrakte-definition-haskell} wurde bereits erwähnt, dass sich für den Mealy-Typen nützliche Klassen-Instanzen implementieren lassen. Die konkreten Implementierungen für den Mealy-Typen wurden übersprungen aber wir werden in diesem Kapitel wesentlichen äquivalente Implementierungen für unseren Typen |RenderSystem| entwickeln. Aus Platzgründen werden nur die jeweils minimal notwendigen Definitionen angegeben, vollständige Definition finden sich im Modul |RenderSystem|. Praktische Anwendungen werden mit unter in \fref{chap:anwendungsbeispiele} gegeben, doch kann auch hier aus Platzgründen keine umfassende Übersicht gegegeben werden. Umfassende Beispiele finden sich ebenso im Projekt beispielsweise im Modul |Yage.Rendering.Pipeline.Deferred|.


%%%
% Functor
%%%

\paragraph{Functor} 
Funktoren, im Folgenden Functor genannt, beschreiben in der Kategorientheorie eine strukturerhaltene Abbildung (Homomorphismus) zwischen zwei Kategorien. In Haskell ist die Klasse der Functoren wie in \fref{lst:class-functor} definiert. Das erste intuitive Verständnis eines |Functor|s in Haskell ist eine Containerstruktur deren Inhalt von einem Typen |a| auf den Typen |b| überführt werden kann, ähnlich dem |map| für die Listen. Zur Klasse der Functoren gehören darüber hinaus noch weit mehr Strukturen. Zum Beispiel lassen sich auch die Ergebnisse von Funktionen homomorph in eine andere Kategorie überführen ohne die Struktur der Berechnung zu verändern.

\begin{haskell}[label={lst:class-functor},caption={Functor Klasse\protect\footnotemark},nolol]
class Functor f where
  fmap :: (a -> b) -> f a -> f b 
\end{haskell}
\footnotetext{http://hackage.haskell.org/package/base-4.7.0.2/docs/Data-Functor.html}

\Fref{lst:rendersystem-functor} definiert eine Functor Instanz für den |RenderSystem| Typen. Beschrieben wird die Überführung des Ergebnistypens |o| in einen neuen Ergebnistypen |c| (siehe konkrete Signatur für |fmap|). Die Implementierung verdeutlich, dass die Struktur der Berechnung nicht verändert wird. Es wird lediglich der Ausgabetyp des |RenderSystem| in einen neuen Ausgabetyp überführt. Diese Functor Instanz ermöglicht es beispielsweise den Ausgabetypen eines |RenderPass|es, mit Hilfe einer Tranformationsfunktion |f| als Adapter, auf den Eingabetypen eines nachfolgenden |RenderPass| zu überführen.

\begin{haskell}[label={lst:rendersystem-functor},caption={Functor Instanz für RenderSystem}]
instance Monad m => Functor (RenderSystem m i) where
  fmap :: (o -> c) -> RenderSystem m i o -> RenderSystem m i c
  fmap f (RenderSystem sys) = RenderSystem (sys >=> \ (o,sys') -> return (f o, fmap f sys'))
\end{haskell}


%%%
% Applicative
%%%

\paragraph{Applicative}
Applikative, im Folgenden Applicatives, sind eine spezielle Ausprägung von Functoren. Ein Applicative wird über zwei Operationen definiert: |pure| bettet einen Wert in den Kontext |f| ein und der Sequenz-Operator |(<*>)| wendet eine Berechnung im Kontext |f| auf einen Wert in |f| an und kombiniert das Resultat \parencite[Kapitel~2]{Paterson2008}. Applicative sind weniger mächtig als Monaden, da sie nur das Sequenzieren von kontextfreien Operationen erlauben. Sie werden gerne dazu verwendet kleine Bausteine als Kombinatoren zu bauen um sie kontextfrei aufeinander anzuwenden. In Haskell sind Applicatives wie in \fref{lst:class-functor} definiert.

\begin{haskell}[label={lst:class-functor},caption={Applicative Klasse\protect\footnotemark},nolol]
class Functor f => Applicative f where
  pure :: a -> f a
  (<*>) :: f (a -> b) -> f a -> f a
\end{haskell}
\footnotetext{http://hackage.haskell.org/package/base-4.7.0.2/docs/Control-Applicative.html}

Für unser |RenderSystem| definieren wir in \fref{lst:rendersystem-applicative} die Applicative Instanz. Auch hier wird anhand der Implementierung von |(<*>)| deutlich, dass die in |sysf| eingebettete Funktion auf den in |sysa| eingebetteten Wert ohne Berücksichtung des Kontext angewendet wird. |pure| entspricht einem RenderSystem, dass ungeachtet der Eingabe konstant den Wert |b| zurück liefert und unter keinen Umständen das Verhalten wechselt. Mit Hilfe der Appliacative Instanz des |RenderSystem|-Typens können wir mehrere Rendersysteme zu einer größeren Struktur kombinieren, sofern die Rendersysteme nicht von einander direkt abhängen. \Fref{lst:rendersystem-applicative-beispiel} zeigt an einem Beispiel, wie über die Applicative Instanz eine Definition eines RenderSystems . Dieses RenderSystem |sceneToTexture| lässt sich dann über die die Applicative Kombinatoren zu einem RenderSystem kombinieren, dass eine Szene in eine Cubemap rendert. Dynamisch erzeugte Cubemaps werden oft als Environment-Maps (Umgebungstexturen) verwendet, um dynamische Spiegelungen der Umgebung auf Objekten darzustellen.

\begin{haskell}[label={lst:rendersystem-applicative},caption={Applicative Instanz für RenderSystem}]
instance Monad m => Applicative (RenderSystem m i) where
  pure b = r where r = RenderSystem (return . const (b,r))
  RenderSystem sysf <*> RenderSystem sysa = RenderSystem (\i -> do
    (f, mf) <- sysf i
    (a, ma) <- sysa i
    return (f a, mf <*> ma))
\end{haskell}

\begin{haskell}[label={lst:rendersystem-applicative-beispiel},caption={Applicative RenderSystem Beispiel}]
data Cube a = CubeTexture{left,right,top,bottom,front,back :: a}

sceneToTexture :: Camera -> RenderPass IO Scene Texture
sceneToTexture = ...

scene :: Scene
scene = ...

-- Cube with cameras along the +/- world axis directions
cubeViews :: Cube Camera
cubeViews = ...

-- Renders a scene into a cube map
sceneToCubeTexture :: RenderPass IO Scene (Cube Texture)
sceneToCubeTexture = Cube
	`fmap` sceneToTexture (left cubeViews)
	<*> sceneToTexture (right  cubeViews)
	<*> sceneToTexture (top    cubeViews)
	<*> sceneToTexture (bottom cubeViews)
	<*> sceneToTexture (front  cubeViews)
	<*> sceneToTexture (back   cubeViews)
\end{haskell}


%%%
% Profunctor
%%%

\paragraph{Profunctor}
Während die vorhergehenden Klassen nur Operationen auf dem letzten Argument des Typens definierten, gehört unser unser |RenderSystem| Typ auch noch weiteren Klassen an, die sich für die letzten beiden Argumente definieren lassen. Ein Functor in zwei Argumenten wird auch Bifunctor genannt. Jedes Argument gehört einer eigenen Functor-Kategorie an, die auch voneinander unterschiedlich sein können aber nicht müssen. Zusammmen bilden die beiden Functor-Kategorien ($C$ und $D$) eine Produktkategorie ($C \times D$) \parencite{MacLane1998}. Ein spezieller Bifunctor ist der Profunctor, dessen erstes Argument contravariant und zweites Argument covariant ist. Ein covarianter Functor entspricht der üblichen Haskell |Functor| Klasse mit einem Morphismus von der Ausgangskategorie hin zur Zielkategorie. Die Contravarianz entspricht dahingehend einem umgekehrten Morphismus, von der Zielkategorie zur Ausgangskategorie. Die Haskell Definition der Klasse |Profunctor| findet sich in \fref{lst:class-profunctor}

\begin{haskell}[label={lst:class-profunctor},caption={Profunctor Klasse\protect\footnotemark},nolol]
class Profunctor p where
  dimap :: (a -> b) -> (c -> d) -> p b c -> p a d
\end{haskell}
\footnotetext{https://hackage.haskell.org/package/profunctors-3.2/docs/Data-Profunctor.html}

Anschaulicher wird die Funktionsweise eines |Profunctor|s wieder bei der Implementierung der |Profunctor| Instanz für unser |RenderSystem|. Das zweite covariante Argument unseres Bifunctors ist unser Parameter |o| für den Ausgabetypen des |RenderSystems| und wird analog zu der Functor Instanz implementiert, mit |g| als Morphismus. Der neue erste Parameter unseres Bifunctors entspricht dem contravarianten Argument. Der umgekehrte Morphismus |f| wird im Gegenzug auf den Eingabewert angewendet, bevor das entsprechende RenderSystem mit der Eingabe ausgeführt wird. Mit der Profunctor Instanz können wir nicht nur elegant den Ausgabetyp unseres Rendersystems konvertieren, sondern auch auch zu einem allgemein definierten Rendersystem neue Varianten erzeugen die auf unterschiedlichen Eingaben arbeiten, solange wir eine Konvertierungsfunktion für die Eingabe definieren können. Die interne Funktionsweise des Rendersystems bleibt trotz etwaiger Konvertierungen von Ein- und Ausgaben die selbe, es werden lediglich die Schnittstellen einem externen Format angepasst.

\begin{haskell}[label={lst:rendersystem-profunctor},caption={Profunctor Instanz für RenderSystem}]
instance Monad m => Profunctor (RenderSystem m) where
  dimap :: (i -> a) -> (b -> o) -> RenderSystem m a b -> RenderSystem m i o
  dimap f g (RenderSystem sys) = RenderSystem (\i -> do
    (o,sys') <- sys (f i)
    return (g o, dimap f g sys'))
\end{haskell}


%%%
% Category
%%%

\paragraph{Category}

Die |Category| beschreibt die Klasse von Strukturen deren Morphismus sich über beide Argumente komponieren lassen. Die grundlegenste |Category| in Haskell ist die der Funktionen und ihre Komponierbarkeit. Unäre Funktionen |(->)| beschreiben einen Morphismus der das Argument der Funktion einem Funktionswert zuordnet. Unäre Funktionen sind in Haskell analog zur mathematischen Funktionskomposition komponierbar. Dies wird dadurch ausgedrückt, dass unäre Funktionen der |Category| Klasse angehören. In \fref{lst:class-category} wird die Definition der Klasse ausgeschrieben. Sie besteht aus dem Identitätsmorphismus |id| und der rechts zu links Komposition |(.)|.

\begin{haskell}[label={lst:class-category},caption={Category Klasse\protect\footnotemark},nolol]
class Category cat where
  id :: cat a a
  (.) :: cat b c -> cat a b -> cat a c
\end{haskell}
\footnotetext{https://hackage.haskell.org/package/base-4.7.0.2/docs/Control-Category.html}

Auch unser Rendersystem beschreibt einen Morphismus von der Eingabe |i| zur Ausgabe |o|. Folglich gehört auch unser |RenderSystem| Typ der Klasse der Kategorien an. In \fref{lst:rendersystem-category} listet die Implementierng der |Category| Klasse für unser |RenderSystem|. Unser Identitätsmorphismus ist ein Rendersystem, dass die Eingabe unverändert ausgibt. Unsere Komposition ist dadurch implementiert, dass wir zuerst die am weitesten rechts stehende Eingabe |a| in unser rechtes |RenderSystem| eingeben und die entsprechende Ausgabe |b| an das linke |RenderSystem| weiter leiten um die finale Ausgabe |c| zu erhalten. Da die jeweiligen Rendersysteme sich dynamisch verändern können, werden die neu erzeugten RenderSysteme ebenso komponiert (|mbc' . mab'|). 

Die erlaubt es uns auf elegenate Weise unterschiedliche Stufen eines RenderSystems hintereinander zu schalten und zu einem größeren Gesamtsystem zu komponieren. \Fref{lst:rendersystem-komposition-beispiel} zeigt an einem Beispiel, wie sich durch die Komposition zweier unabhängig von einander definierten Textur-Filter ein neuer Filter |bloom| erzeugt werden kann.

\begin{haskell}[label={lst:rendersystem-category},caption={Category Instanz für RenderSystem}]
instance Monad m => Category (RenderSystem m) where
  id = RenderSystem (return . (,id))
  RenderSystem mbc . RenderSystem mab = RenderSystem (\a -> do
    (b, mab') <- mab a
    (c, mbc') <- mbc b
    return (c, mbc' . mab'))
\end{haskell}

\begin{haskell}[label={lst:rendersystem-komposition-beispiel},caption={Beispiel einer Komposition von RenderSystem}]
-- Passes colors with Luma above 'threshold'
lumaFilter :: Float -> RenderSystem IO Texture Texture
lumaFilter treshold = ...

-- Filters 'Texture' with a gaussian-kernel of kernelsize x kernelsize
gaussianFilter :: Int -> RenderSystem IO Texture Texture
gaussianFilter kernelsize = ...

bloom :: RenderSystem IO Texture Texture
bloom = gaussFilter 64 . lumaFilter 0.4
\end{haskell}


%%%
% Semigroup
%%%

\paragraph{Semigroup}

Eine \textit{Semigroup} bzw. eine \textit{Halbgruppe} ist eine algebraische Struktur mit einer assoziativen binären Verknüpfung. Sie gilt als Verallgemeinerung eines Monoiden, für die kein neutrales Element benötigt wird und respektive einer \textit{Group} bzw \textit{Gruppe}, für die kein inverses und kein neutrales Element benötigt wird.

\begin{haskell}[label={lst:class-semigroup},caption={Semigroup Klasse\protect\footnotemark},nolol]
class Semigroup a where
  (<>) :: a -> a -> a -- fuer Monoiden 'mappend'
\end{haskell}
\footnotetext{https://hackage.haskell.org/package/semigroups-0.16.2.2/docs/Data-Semigroup.html}

Die Implementierung in \fref{lst:rendersystem-semigroup} verknüpft die beiden |RenderSystem|e, in dem beide auf der gleichen Eingabe sequenziell ausführt werden. Die Resultate werden abschließend mit |(<>)| verknüpft.

\begin{haskell}[label={lst:rendersystem-semigroup},caption={Semigroup Instanz für RenderSystem}]
instance (Applicative m, Semigroup o) => Semigroup (RenderSystem m i o) where
	sysX <> sysY =  RenderSystem (\i -> liftA2 (<>) (runRenderSystem sysX i) (runRenderSystem sysY i))
\end{haskell}

\Fref{lst:rendersystem-semigroup-beispiel} ist ein Beispiel dafür, wie die Resulate aus mehreren Render-Schritten zu einer größeren assoziative Struktur zusammengefügt werden können. In diesem Fall wird mehrfach |downsample| auf auf einer Textur ausgeführt, um anschließend die herunter gerechneten Texturen zu einer Mipmap-Chain zu kombinieren. 

\begin{haskell}[label={lst:rendersystem-semigroup-beispiel},caption={Beispielanwendung Semigroup für RenderSystem}]
import Data.List.NonEmpty
-- | MipmapChain mit einem Basis Element und einer Liste von Mipmap Stufen
-- Instanz von Semigroup
type MipmapChain = NonEmpty

mkBase :: b -> MipmapChain b
mkBase b = b :| []

downsample :: Int -> RenderSystem IO Texture Texture
downsample factor = ...

generateMipmap :: RenderSystem IO Texture (MipmapChain Texture)
generateMipmap = fmap mkBase id 
	<> fmap mkBase (downsample (2^1)) 
	<> fmap mkBase (downsample (2^2)) 
	<> fmap mkBase (downsample (2^3))
\end{haskell}


%%%
% Arrow
%%%

\paragraph{Arrow}

|Arrows| sind eine weitere abstrakte Darstellungsform von Morphismen. Während |Category| die Komponierbarkeit von Morphismen beschreibt, beschreiben Arrows den Morphismus an sich. In Haskell wird ein Arrow über die Operation |arr| und |first| definiert. Zusätzlich gehört zur Arrow Klasse auch die Kompositionsoperation, welche aber schon durch die Spezialisierung von |Category| definiert ist. |arr| stellt den direkten Bezug zu dem grundlegenden Morphismus der unären Funktionen |(->)| und der Arrow Struktur her und hebt einen einfachen Morphismus in den Kontext des |Arrow|s. |first| ist eine Variante, neben dem gespiegelten |second|, der sogenannten Sideline Operation \parencite[Kapitel 1]{Asada2010}. Über |first| kann ein Morphismus |a b c| auf die erste Komponente des Argumentes angewendet werden, während doe zweite Komponente unverändert durchgereicht wird. Erwähnenswert ist abschließend, dass jeder |Arrow| auch als |Profunctor| dargestellt werden kann \parencite[Kapitel 3]{Asada2010}.

\begin{haskell}[label={lst:class-arrow},caption={Arrow Klasse\protect\footnotemark},nolol]
class Category a => Arrow a where
  arr :: (b -> c) -> a b c
  first :: a b c -> a (b, d) (c, d)
\end{haskell}
\footnotetext{https://hackage.haskell.org/package/base-4.7.0.2/docs/Control-Arrow.html}


\begin{haskell}[label={lst:rendersystem-arrow},caption={Arrow Instanz für RenderSystem}]
instance Monad m => Arrow (RenderSystem m) where
  arr f = RenderSystem (\i -> return (f i, arr f))
  first sys = RenderSystem (\ (b,d) -> do
    (c, sys') <- runRenderSystem sys b
    return ((c,d), first sys'))
\end{haskell}

%%%
% ArrowChoice
%%%

\paragraph{ArrowChoice}

Bevor wir zu einem abschließenden Beispiel für die Anwendung der |Arrow| Implementierung unter Verwendung der Arrow-Notation \footnotemark kommen, werden wir im folgenden noch für unser |RenderSystem| die |ArrowChoice| Klasse implementieren um weitere Möglichkeiten der Arrow Notation nutzen zu können. |Arrow|s die auch |ArrowChoice| implementieren können in der Arrow Notation |if| und |case| Konstrukte als syntaktischen Zucker verwenden. Die Definition der |ArrowChoice| Klasse ist in \fref{lst:class-arrowchoice} angeben. |left| beschreibt dabei eine Operation die eine Operation auf das Argument anwendet, wenn das Argument mit |Left| markiert wurde. Wurde das Argument mit |Right| markiert bleibt es unverändert.

\footnotetext{https://downloads.haskell.org/~ghc/7.8.2/docs/html/users\_guide/arrow-notation.html}

\begin{haskell}[label={lst:class-arrowchoice},caption={ArrowChoice Klasse\protect\footnotemark},nolol]
class Arrow a => ArrowChoice a where
  left :: a b c -> a (Either b d) (Either c d)
\end{haskell}
\footnotetext{https://hackage.haskell.org/package/base-4.7.0.2/docs/Control-Arrow.html\#g:5}

Die Implementierung von |ArrowChoice| für unser |RenderSystem| in \fref{lst:rendersystem-arrowchoice} ist trivial. Wurde die Eingabe mit |Left| markiert, wenden wir unser |RenderSystem| auf die Eingabe an und markieren die Ausgabe wiederum mit |Left|. Mit |Right| markierte Eingaben passieren das RenderSystem unverändert und ohne Seiteneffekte.

\begin{haskell}[label={lst:rendersystem-arrowchoice},caption={ArrowChoice Instanz für RenderSystem}]
instance Monad m => ArrowChoice (RenderSystem m) where
  left sys = RenderSystem (\case
    Left i  -> do
      (b, sys') <- runRenderSystem sys i
      return (Left b, left sys'))
    Right i -> return (Right i, left sys)
\end{haskell}
